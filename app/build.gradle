buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven {
			url "https://maven.google.com"
		}
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
	}
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def secrets = getSecrets()
def appProperties = getAppProperties(projectDir)

android {
	compileSdkVersion 27
	buildToolsVersion "27.0.3"

	defaultConfig {
		applicationId "uk.co.markormesher.quicktimer"
		minSdkVersion 16
		targetSdkVersion 27
		versionCode = appProperties['VERSION_CODE'].toInteger()
		versionName = appProperties['VERSION_NAME']
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	signingConfigs {
		main {
			storeFile secrets.isEmpty() ? null : file(secrets['KEYSTORE_PATH'])
			storePassword secrets['KEYSTORE_PASSWORD']
			keyAlias secrets['KEYSTORE_KEY_ALIAS']
			keyPassword secrets['KEYSTORE_KEY_PASSWORD']
		}
	}

	buildTypes {
		release {
			//noinspection GroovyAssignabilityCheck
			signingConfig signingConfigs.main
			minifyEnabled false
			buildConfigField "boolean", "DEBUG_MODE", "false"
			debuggable false
		}

		debug {
			//noinspection GroovyAssignabilityCheck
			signingConfig signingConfigs.main
			minifyEnabled false
			buildConfigField "boolean", "DEBUG_MODE", "true"
			debuggable true
		}
	}

	sourceSets {
		main.java.srcDirs += 'src/main/kotlin'
	}
}

dependencies {
	def supportLibraryVersion = '27.1.1'

	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
	implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
	implementation "com.android.support:design:$supportLibraryVersion"
	implementation 'uk.co.markormesher:android-fab:2.3.1'

	testImplementation 'junit:junit:4.12'
	androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
}
